# boat class items in rowing
SELECT ?item ?itemLabel ?itemDescription ?shortName ?sweepscullLabel ?rowers ?cox ?mass ?length ?icon WHERE {
	?item wdt:P31 wd:Q26211803; wdt:P279 ?sweepscull .
	FILTER(?sweepscull = wd:Q18630808 || ?sweepscull = wd:Q1256290) .
	OPTIONAL { ?item wdt:P1813 ?shortName }
	OPTIONAL { ?item p:P1873 [ ps:P1873 ?rowers; pq:P2868 wd:Q13382576 ] }
	OPTIONAL { ?item p:P1873 [ ps:P1873 ?cox; pq:P2868 wd:Q1690874 ] }
	OPTIONAL { ?item wdt:P2067 ?mass }
	OPTIONAL { ?item wdt:P2043 ?length }
	OPTIONAL { ?item wdt:P2910 ?icon }
	SERVICE wikibase:label { bd:serviceParam wikibase:language "es,en" }

} ORDER BY ASC(?rowers) ASC(?sweepscullLabel) ASC(?cox)
